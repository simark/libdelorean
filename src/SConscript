# author: Philippe Proulx <eepp.ca>


import os.path


Import('root_env')

target = 'delorean'

main_sources = [
    'AbstractHistory.cpp',
    'AbstractHistoryFile.cpp',
    'HistoryFileSink.cpp',
    'HistoryFileSource.cpp',
]
ex_sources = [
    'UnknownIntervalType.cpp',
]
interval_sources = [
    'AbstractInterval.cpp',
    'StringInterval.cpp',
]
node_sources = [
    'AbstractNodeSerDes.cpp',
    'AlignedNodeSerDes.cpp',
    'AbstractNodeCache.cpp',
    'DirectMappedNodeCache.cpp',
    'LruNodeCache.cpp',
    'Node.cpp',
]

subs = [
    ('.', main_sources),
    ('ex', ex_sources),
    ('interval', interval_sources),
    ('node', node_sources),
]

sources = []
for base, files in subs:
    sources += [os.path.join(base, f) for f in files]

lib = root_env.SharedLibrary(target=target, source=sources,
                             LIBS='boost_system')
Return('lib')
